#BYM model for the random effects = U + V, V ~ Normal, U ~ CAR

model
{
  for (i in 1 : N) {
      O[i]  ~ dpois(mu[i])
      log(mu[i]) <- log(E[i]) + alpha + V[i] + U[i]
      V[i] ~ dnorm(0, prec.v)                        # unstructured RE
      RR[i] <- exp(alpha + V[i] + U[i])              # Area-specific RR
      resRR[i] <- exp(V[i]+ U[i])                    # Area-specific residual RR
  
      #probability that the area-specific RR is above the average
      # <=> probability that the area-specific residual RR is above the average
      probaresRR[i]<-step(resRR[i]-1)
  }
  
  # intrinsic CAR prior on spatial random effects
  U[1:N] ~ car.normal(adj[], weights[], num[], prec.u)
  
  for(k in 1:sumNumNeigh) {weights[k] <- 1 }

  # priors:
  alpha  ~ dflat()                                    # vague prior (small precision=large variance)
  overallRR <- exp(alpha)                           # overall RR across study region

  prec.v ~ dgamma(0.5, 0.0005)
  sigma2.v <- 1/prec.v                             # variance of unstructured area random effects
  
  prec.u ~ dgamma(0.5, 0.0005)                     # prior on precison of spatial area random effects
  sigma2.u <- 1/prec.u                             # conditional variance of spatial area random effects
  sigma2.u.marginal <- sd(U[]) * sd(U[])           # empirical marginal variance of spatial effects
  
  frac.spatial <-  sigma2.u.marginal / ( sigma2.u.marginal + sigma2.v)  # fraction of total variation in log RR
  QR90 <- ranked(resRR[],140)/ranked(resRR[],8)         # 90 percent resRR ratio
}
