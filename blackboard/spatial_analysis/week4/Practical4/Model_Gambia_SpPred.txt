# Practical 4. Malaria in Gambia 
	
model {
   for(i in 1:M) {
      O[i] ~ dbern(p[i])
      logit(p[i]) <- alpha + beta.age[age[i]] + beta.bednet[bednet[i]] +
                                    beta.green*(green[i] - mean(green[])) + beta.phc*phc[i] +  w[village[i]]
   }

   # spatial village-level random effect
   w[1:65] ~ spatial.exp(mu[], x[], y[], v2.inv, phi, kappa)

   for(j in 1:N) {  OR.village[j] <- exp(w[j])    }      # odds ratio of malaria in village j relative to average

   # priors
   for(i in 1:N) { mu[i] <- 0 }

   # alternative priors for random effects variance to check sensitivity
   v2.inv ~ dgamma(0.5, 0.0005)
   v <- 1/sqrt(v2.inv)                     # sd of spatial random effects
 
   # parameters of spatial exponential covariance function   
   
   kappa <- 1

   range ~ dunif(0.9, 273)
   phi <- 3/range

   corr.1km <- exp(-phi*1)        # correlation in log odds of malaria between locations 1 km apart

   # vague priors on regression coefficients
   alpha ~ dnorm(0, 0.0001)
   
   beta.age[1] <- 0          # set coefficient for baseline age group to zero (corner point constraint)
   beta.age[2] ~ dnorm(0, 0.0001)     
   beta.age[3] ~ dnorm(0, 0.0001)     
   beta.age[4] ~ dnorm(0, 0.0001)     
   
   beta.bednet[1] <- 0     # set coefficient for baseline bednet group to zero (corner point constraint)
   beta.bednet[2] ~ dnorm(0, 0.0001)     
   beta.bednet[3] ~ dnorm(0, 0.0001)     
   
   beta.green ~ dnorm(0, 0.0001)     
   
   beta.phc ~ dnorm(0, 0.0001)     
 
   # calculate odds ratios of interest
   OR.age[2] <- exp(beta.age[2])       # odds ratio of malaria for age group 2 vs age group 1 
   OR.age[3] <- exp(beta.age[3])       # odds ratio of malaria for age group 3 vs age group 1       
   OR.age[4] <- exp(beta.age[4])       # odds ratio of malaria for age group 4 vs age group 1       
   
   OR.bednet[2] <- exp(beta.bednet[2])       # odds ratio of malaria for children using untreated bednets   
                                                               # vs children not using bednets
   OR.bednet[3] <- exp(beta.bednet[3])       # odds ratio of malaria for children using treated bednets   
                                                               # vs children not using bednets
   OR.bednet[4] <- exp(beta.bednet[3] - beta.bednet[2])       # odds ratio of malaria for children using treated
                                                                                       # bednets vs children using untreated bednets
   OR.green <- exp(beta.green)        # odds ratio of malaria per unit increase in greenness index of village
   
   OR.phc <- exp(beta.phc)              # odds ratio of malaria for children living in villages belonging to the 
                                                     # primary health care system versus children living in villages not in
                                                     # the health care system

   logit(baseline.prev) <- alpha          # baseline.prev = prevalence of malaria in baseline group (i.e. child
                                                     # in age group 1 (<2yrs), sleeps without bednet, and lives in a village
                                                     # with average greenness index and not in the health care system) 

   PP.untreated <- step(1 - OR.bednet[2])  # probability that using untreated bed net vs no bed net reduces
                                                              # risk of malaria 
   PP.treated <- step(1 - OR.bednet[4])      # probability that using treated vs untreated bed net reduces
                                                              # risk of malaria 

   # spatial prediction			
   

}